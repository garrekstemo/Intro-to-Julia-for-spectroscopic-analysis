# Arrays

Arrays are a collection of elements, usually of the same type but not always.
Arrays in Julia are **mutable**, which means that you can change the elements of an array after it has been created.

```julia
julia> v = [1, 2, 3]  # array of integers
3-element Vector{Int64}:
 1
 2
 3

julia> w = [1.0, 2.0, 3.0]  # array of floats
3-element Vector{Float64}:
 1.0
 2.0
 3.0

julia> a = ["hello", 2.0, 3, false, "ðŸŒµ"]  # mixed array has type Any
3-element Vector{Any}:
    "hello"
    2.0
    3
false
    "ðŸŒµ"
```

Notice that these arrays are of type `Vector{Int64}`, `Vector{Float64}`, and `Vector{Any}`.
The type `Vector` is an alia for a one-dimensional `Array` in Julia.

```julia
julia> typeof(v)
Vector{Int64} (alias for Array{Int64, 1})

julia> typeof(w)
Vector{Float64} (alias for Array{Float64, 1})
```

## Range objects
Range objects can also be used to create arrays.
The object `r = 1:10` is a `UnitRange` object that represents the numbers from 1 to 10 with step size 1.
You can also create a range object with a step size.
For example, `r = 1:2:10` represents the numbers from 1 to 10 with a step size of 2.
This is a `StepRange` object.

We can use the `dump` function to see the structure of an object. Try using `dump(r)` on the range object `r = 1:2:10`
and compare it to a `Vector` object created with `v = [1, 2, 3]`.
We can see that a `Vector` object is a one-dimensional array of elements, while a `UnitRange` object is a range of numbers with a start, stop, and step size.
It's kind of a lazy object that doesn't store all the numbers in memory.
This makes them very memory efficient.
See [this StackOverflow post](https://stackoverflow.com/questions/55438134/creating-arrays-from-ranges-in-julia-without-using-collect) for a detailed explanation.
When you use `collect(r)`, it creates a new array with the elements of the range object.

```julia
julia> v = [1, 2, 3]
3-element Vector{Int64}:
 1
 2
 3

julia> dump(v)
Array{Int64}((3,)) [1, 2, 3]

julia> r = 1:2:10
1:2:9

julia> dump(r)
StepRange{Int64, Int64}:
  start: 1
  stop: 10
  step: 2

julia> dump(1:3)  # UnitRange is even lazier
UnitRange{Int64}
  start: Int64 1
  stop: Int64 3
```

You can also create a range object with more specificity by using the `range` function.
```julia
# create a range with a start value, end value, and step size
range(1, stop=10, step=2)

# create a range with a start value, step size, and length
range(1, step=0.5, length=5)  
```
Check out `?range` in the REPL to see all the options available for creating range objects.


## Indexing and slicing
You can access elements of an array using *indexing*.
Julia uses 1-based indexing, which means that the first element of an array is at index 1, not 0.

```julia
a = ["hello", 2.0, false, "ðŸŒµ", 6]
a[1]  # returns "hello"
a[2]  # returns 2.0
a[end-1]  # returns "ðŸŒµ"
```

Subarrays can be extracted by *slicing* with `i:j` notation.

```julia
a = ["hello", 2.0, false, "ðŸŒµ", 6]
a[1:2]  # returns ["hello", 2.0]
a[2:end]  # returns [2.0, false, "ðŸŒµ", 6]
```

We can also step through the array with a step size.

```julia
a = ["hello", 2.0, false, "ðŸŒµ", 6]
a[1:2:end]  # returns ["hello", false, 6]
```
We can also step *backwards* through the array.

```julia
a = ["hello", 2.0, false, "ðŸŒµ", 6]
a[end:-1:1]  # returns [6, "ðŸŒµ", false, 2.0, "hello"]
```

Elements in a list can be reassigned using their index.

```julia
julia> v = [1, 2, 3]
3-element Vector{Int64}:
 1
 2
 3

julia> v[1] = 10  # change the first element of the array
10

julia> v
3-element Vector{Int64}:
 10
  2
  3
```

### Exercises
1. Consider an array created as follows:

    ```julia
    x = 3
    my_array = [1, 2, x]
    ```
    What happens to `my_array` if `x` is changed after the array is created?

2. In the array `v = [1, 2, 3]`, what happens if you try to change the second element to `1.0`? What about if you try to change it to a string?

3. What is the last element of the range object `1:2:10`?

4. A `BoundsError` occurs when you try to access an element of an array that doesn't exist. Write a code snippet that produces a `BoundsError`. Then fix it.


If you want to iterate over a collection and keep track of the index, you can use the `enumerate` function.

```julia
fruits = ["apple", "banana", "cherry"]
for (index, fruit) in enumerate(fruits)
    println("Index: $index, Fruit: $fruit")
end
```

If you *only* want the index, you can use the `eachindex` function.

```julia
fruits = ["apple", "banana", "cherry"]
for index in eachindex(fruits)
    println("Index: $index")
end
```

There are special functions that allow you to iterate over rows and columns of a matrix

```julia
A = [1 2 3; 4 5 6; 7 8 9]
for row in eachrow(A)
    println(row)
end
for col in eachcol(A)
    println(col)
end
```


## Operations on arrays

Simple operations can extract information about an array or modify it. The length of a `Vector` can be obtained using the `length` function.

```julia
a = ["hello", 2.0, false, "ðŸŒµ", 6]
julia> length(a)
5
```

We can also use the `push!` function to add an element to the end of an array.

```julia
julia> push!(a, "-2")  # add a new element to the end of the array
6-element Vector{Any}:
    "hello"
    2.0
 false
    "ðŸŒµ"
    6
    "-2"
```


## Matrices
Matrices are two-dimensional arrays in Julia. You can create a matrix using the `reshape` function or by using semicolons to separate rows.

```julia
julia> A = [1 2 3; 4 5 6; 7 8 9]  # matrix with semicolons
3Ã—3 Matrix{Int64}:
 1  2  3
 4  5  6
 7  8  9

julia> B = reshape(1:9, 3, 3)  # matrix with reshape
3Ã—3 reshape(::UnitRange{Int64}, 3, 3) with eltype Int64:
 1  4  7
 2  5  8
 3  6  9

julia> A[1, 2]  # indexing a matrix
2

julia> A[2, 3]
6

julia> A[1, :]  # all elements in the first row
3-element Vector{Int64}:
 1
 2
 3

julia> A[:, 1]  # all elements in the first column
3-element Vector{Int64}:
 1
 4
 7
```
You can also use the `size` function to get the dimensions of a matrix.

```julia
julia> size(A)
(3, 3)
julia> size(B)
(3, 3)
```

## Matrix and vector operations
You can perform various operations on matrices and vectors, such as addition, subtraction, multiplication, and division.

```julia
julia> A + B  # matrix addition
3Ã—3 Matrix{Int64}:
  2   6  10
  6  10  14
 10  14  18

julia> A - B  # matrix subtraction
3Ã—3 Matrix{Int64}:
 0  -2  -4
 2   0  -2
 4   2   0

julia> A * B  # matrix multiplication
3Ã—3 Matrix{Int64}:
 14   32   50
 32   77  122
 50  122  194

julia> A / B  # matrix division
3Ã—3 Matrix{Float64}:
 -0.333333   0.666667  -0.0
  2.26667   -7.53333    5.6
 -6.33333    6.66667   -0.0

julia> A'  # transpose of a matrix
3Ã—3 adjoint(::Matrix{Int64}) with eltype Int64:
 1  4  7
 2  5  8
 3  6  9

julia> A * v  # matrix-vector multiplication. What do you think will happen?
3-element Vector{Int64}:
 14.0
 32.0
 50.0

julia> v * A  # what do you think will happen?
```

There are more linear algebra operations available if you use the `LinearAlgebra` package.


## Broadcasting
Broadcasting allows you to apply a function to each element of an array or collection.
You can use the dot `.` operator to apply a function element-wise.
This is also known as [vectorizing](https://docs.julialang.org/en/v1/manual/functions/#man-vectorized) a function.
For example, you can perform element-wise operations using the `.*`, `./`, and `.^` operators.

```julia
julia> v = [1, 2, 3]
3-element Vector{Int64}:
 1
 2
 3

julia> v .+ 1  # add 1 to each element of v
3-element Vector{Int64}:
 2
 3
 4

julia> v .+ v  # add v to itself
3-element Vector{Int64}:
 2
 4
 6
```
You can multiply a scalar with a vector without broadcasting.
The notation is perhaps intuitive if you are used to mathematical notation.
You can write both `2 * v` and `2v` to multiply a vector by a scalar.
Actually this works for most variables.

Broadcasting also works with matrices.
The `Matrix` type is an alias for a two-dimensional `Array`, denoted as `Array{T, 2}`.

```julia
A = [1 2 3; 4 5 6; 7 8 9]
B = A
julia> A .* B  # element-wise multiplication
3Ã—3 Matrix{Int64}:
    1   4   9
    16  25  36
    49  64  81
```
Note the difference between `A * B` and `A .* B`. The first one is *matrix* multiplication, while the second one is *element-wise* multiplication.

Broadcasting also applies to functions.
Let's start with a familiar function that adds 1 to a number.

```julia
julia> f(x) = 1 + x
f (generic function with 1 method)

julia> f(1)
2
```

What happens if we try to apply this function to a vector?
```julia
julia> v = [1, 2, 3]
3-element Vector{Int64}:
 1
 2
 3

julia> f(v)
ERROR: MethodError: no method matching +(::Int64, ::Vector{Float64})
For element-wise addition, use broadcasting with dot syntax: scalar .+ array
The function `+` exists, but no method is defined for this combination of argument types.

Closest candidates are:
  +(::Any, ::Any, ::Any, ::Any...)
   @ Base operators.jl:596
  +(::Real, ::Complex{Bool})
   @ Base complex.jl:322
  +(::Array, ::Array...)
   @ Base arraymath.jl:12
  ...

Stacktrace:
 [1] f(x::Vector{Float64})
   @ Main ./REPL[95]:1
 [2] top-level scope
   @ REPL[99]:1
```
The error message is telling us that the function `+` does not know how to add a scalar to a vector.
Broadcasting *vectorizes* the function. This is also known as "element-wise" operations, "vectorized" operations, or "broadcasting".

```julia
julia> f.(v)  # apply f to each element of v
3-element Vector{Int64}:
 2
 3
 4
```

You can also use broadcasting with other functions.

```julia
julia> sin.(v)  # apply sin to each element of v
3-element Vector{Float64}:
 0.8414709848078965
 0.9092974268256817
 0.1411200080598672
```

It is also possible to define a function that takes a vector as input and returns a vector as output.

```julia
v = [1, 2, 3]
f(x) = x .+ 1  # broadcasting with dot operator
f(v)  # returns [2, 3, 4]
```

or using the `@.` macro

```julia
julia> @. f(x) = x + 1  # broadcasting with @. macro
f (generic function with 1 method)
julia> f(v)
3-element Vector{Int64}:
 2
 3
 4
```

### Exercises
1. Write a function that takes a vector of numbers `v` and a positive integer `n`. The function should return a new vector that rotates the elements of `v` by `n` positions.
An element should go back to the beginning of the vector if it goes past the end.

    ```julia
    function rotate(v, n)
        # add code here
    end
    
    using Test
    v = [1, 2, 3]
    @test rotate(v, 1) == [2, 3, 1]
    ```


## Array comprehensions
There are many ways to programmatically create arrays in Julia.
One way is to apply a function to each element of an array.
For example, if we want to apply a function `square` to each element of a vector containing integers from 0 to 5, we can use the `map` function.

```julia
square(x) = x^2
map(square, 0:5)  # returns [0, 1, 4, 9, 16, 25]
```
Another method is to filter the elements of an array.
For example, if we want to filter the even numbers from a vector containing integers from 0 to 5, we can use the `filter` function and the function `iseven` that returns true if a number is even.

```julia
filter(iseven, 0:5)  # returns [0, 2, 4]
```

Sometimes we don't want a lazy array. If we want each element in an array to be stored in memory, we can use the `collect` function.
```julia
collect(0:5)  # returns [0, 1, 2, 3, 4, 5]
```

Finally, array comprehensions combine mapping and filtering in a single expression.
In the following example, we create a new array with the squares of the even numbers from 0 to 5.

```julia
[x^2 for x in 0:5 if iseven(x)]  # returns [0, 4, 16]
```

Finally, comprehensions can be used to create multi-dimensional arrays.

```julia
julia> [i + j for i in 1:3, j in 1:3]  # returns a 3x3 matrix
3Ã—3 Matrix{Int64}:
 2  3  4
 3  4  5
 4  5  6
 ```
 

### Exercises
1. Use a comprehension to create an array of numbers 1 to 100 that are divisible by 7.

2. Use an array comprehension to find all of the vowels in a string.

## Problems

1. Make a vector with the numbers 1 to 10 using array comprehension.
2. Use the vector that you made in the previous quesion. Write code that sets every even-indexed element of a vector `v` to 0. A list of `n` zeros can be created with `zeros(n)` or `fill(0, n)`.
*Challenge: Do this in one line of code without defining a new function!*